#import "storage.jsligo" "Storage"
#import "common/errors.jsligo" "Errors"

const amount_must_be_subscription_value = (amount: tez, price) : unit =>
  assert_with_error(
    amount == price,
    Errors.amount_must_be_subscription_value
  );

const must_be_admin = (admin: address) : unit =>
  assert_with_error(
    Tezos.get_sender() == admin,
    Errors.incorrect_sender
  );

const signature_must_be_valid = (public_key: key, signature: signature, message: bytes) : unit =>
  assert_with_error(
    Crypto.check(public_key, signature, message),
    Errors.invalid_signature
  );

const must_have_enough_balance = (amount: tez) : unit => assert_with_error(
  Tezos.get_balance() >= amount,
  Errors.low_balance
);

const policy_id_must_exist = (policies: Storage.Types.ledger, policy_id: nat) : unit => assert_with_error(
 Big_map.mem(policy_id, policies),
 Errors.token_not_found
);

const message_must_be_valid = (message: bytes): unit => {
  const message_prefix = Bytes.sub(0 as nat, 2 as nat, message);
  return assert_with_error(message_prefix == (bytes(192) as bytes), Errors.invalid_message);
}

const pricing_must_be_active = (isValid): unit => assert_with_error(
  isValid,
  Errors.pricing_not_active
);

const must_be_for_this_contract = (contract: address): unit => assert_with_error(
  Tezos.get_self_address() == contract,
  Errors.incorrect_contract
);
